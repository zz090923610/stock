PLEV	FOLDER
FIN	qa/ma
PLFDOUT qa/candle_stick_shape_analysis

# 基本列
OUTCOLS date,YIN,YANG,LARGEYANG,MIDYANG,SMALLYANG,LARGEYIN,MIDYIN,SMALLYIN
JUDGE YIN open>close                                           # 阴线
JUDGE YANG open<close                                          # 阳线
SUBC CLOP close open                                                # 收盘价-开盘价
SUBC OPCL open close                                                # 开盘价-收盘价
SUBC VIBRATION high low                                              # 振幅
JUDGE LARGEYANG CLOP>=.03*open                                 # 大阳线 实体低值 3%
JUDGE MIDYANG (CLOP>=.015*open)&(CLOP<.03*open)                # 中阳线 实体低值 1.5%-3%
JUDGE SMALLYANG (CLOP>0)&(CLOP<.015*open)                      # 小阳线 实体低值 0-1.5%
JUDGE LARGEYIN OPCL>=.03*close                                 # 大阴线 实体低值 3%
JUDGE MIDYIN (OPCL>=.015*close)&(OPCL<.03*close)               # 中阴线 实体低值1.5%-3%
JUDGE SMALLYIN (OPCL>0)&(OPCL<.015*close)                      # 小阴线 实体低值 0-1.5%
TRANSLATE YIN:阴线, YANG:阳线, VIBRATION:振幅, LARGEYANG:大阳线
TRANSLATE MIDYANG:中阳线, SMALLYANG:小阳线, LARGEYIN:大阴线
TRANSLATE MIDYIN:中阴线, SMALLYIN:小阴线
# 实体
OUTCOLS BOXHIGH,BOXLOW,BOXLENGTH
APPLYH BOXHIGH max open close                                       # 实体高值
APPLYH BOXLOW min open close                                        # 实体低值
APPLYH BOXLENGTH abs CLOP                                           # 实体长度
TRANSLATE BOXHIGH:实体高值, BOXLOW:实体低值, BOXLENGTH:实体长度

# 上下影线
OUTCOLS UPPERSHADOW,LOWERSHADOW
SUBC UPPERSHADOW high BOXHIGH                                       # 上影线长
SUBC LOWERSHADOW BOXLOW low                                         # 下影线长
TRANSLATE UPPERSHADOW:上影线长, LOWERSHADOW:下影线长

# 特殊单日线型
OUTCOLS CROSS,TSHAPE,REVERSETSHAPE,ONESHAPE
JUDGE CROSS (open==close)&(UPPERSHADOW>0)&(LOWERSHADOW>0)              # 十字线
JUDGE TSHAPE (open==close)&(UPPERSHADOW<.00001)&(LOWERSHADOW>0)        # T字线
JUDGE REVERSETSHAPE (open==close)&(UPPERSHADOW>0)&(LOWERSHADOW<.00001) # 反T字线
JUDGE ONESHAPE (open==close)&(UPPERSHADOW<.00001)&(LOWERSHADOW<.00001) # 一字线
TRANSLATE CROSS:十字线, TSHAPE:T字线, REVERSETSHAPE:反T字线, ONESHAPE:一字线

# 特殊开盘
OUTCOLS JUMPHIGHOPEN,JUMPLOWOPEN
SHIFT PREV_CLOSE close 1
JUDGE JUMPHIGHOPEN open>PREV_CLOSE                             # 跳高开盘
JUDGE JUMPLOWOPEN open<PREV_CLOSE                              # 跳低开盘
TRANSLATE JUMPHIGHOPEN:跳高开盘, JUMPLOWOPEN:跳低开盘

# 其他
OUTCOLS LARGEK,MIDK,SMALLK
JUDGE LARGEK LARGEYIN|LARGEYANG                                # 大K线
JUDGE MIDK MIDYIN|MIDYANG                                      # 中K线
JUDGE SMALLK SMALLYIN|SMALLYANG|CROSS                          # 小K线
TRANSLATE LARGEK:大K线, MIDK:中K线, SMALLK:小K线

# 组合K线指标

# 1 晨十字星
OUTCOLS MORNINGCROSS
TRANSLATE MORNINGCROSS 晨十字星

SHIFT PREV_YIN YIN 1
SHIFT PREV_2_YIN YIN 2
SHIFT PREV_CROSS CROSS 1
SHIFT PREV_CLOSE close 1
SHIFT PREV_2_CLOSE close 2
SHIFT PREV_2_OPEN open 2
# 判定: T-2阴线　T-1十字线　T阳线 T收盘价于T-2开盘价收盘价之间　T-1收盘价低于T-2收盘价
JUDGE MORNINGCROSS PREV_2_YIN&PREV_CROSS&YANG&(close>PREV_2_CLOSE)&(close<PREV_2_OPEN)&(PREV_CLOSE<PREV_2_CLOSE)

# 2 清晨之星
OUTCOLS MORNINGSTAR
TRANSLATE MORNINGSTAR 清晨之星
SHIFT PREV_SMALLYIN SMALLYIN 1
SHIFT PREV_SMALLYANG SMALLYANG 1
JUDGE MORNINGSTAR PREV_2_YIN&YANG&(close>PREV_2_CLOSE)&(close<PREV_2_OPEN)&(PREV_CLOSE<PREV_2_CLOSE)&(PREV_SMALLYIN|PREV_SMALLYANG)

# 3 好友反攻
OUTCOLS FRIENDLYFIRE
TRANSLATE FRIENDLYFIRE 好友反攻
SHIFT PREV_LARGEYIN LARGEYIN 1
SUBC CLOSEDISTANCE close PREV_CLOSE
APPLYH CLOSEDISTANCE abs CLOSEDISTANCE
JUDGE FRIENDLYFIRE PREV_LARGEYIN&JUMPLOWOPEN&(MIDYANG|LARGEYANG)&(CLOSEDISTANCE<=.005*close)

# 4 曙光初现
OUTCOLS HOPEOFDAWN
TRANSLATE HOPEOFDAWN 曙光初现
SHIFT PREV_MIDYIN MIDYIN 1
JUDGE HOPEOFDAWN (PREV_LARGEYIN|PREV_MIDYIN)&(LARGEYANG|MIDYANG)&(close>=PREV_CLOSE+.5*BOXLENGTH)

# 5 旭日东升
OUTCOLS RAISINGSUN
TRANSLATE RAISINGSUN 旭日东升
SHIFT PREV_OPEN open 1
JUDGE RAISINGSUN (PREV_LARGEYIN|PREV_MIDYIN)&JUMPHIGHOPEN&(LARGEYANG|MIDYANG)&(close>PREV_OPEN)

# 6 倒锤头线
OUTCOLS REVERSEHAMMER
TRANSLATE REVERSEHAMMER 倒锤头线
JUDGE REVERSEHAMMER (SMALLYIN|SMALLYANG)&(UPPERSHADOW>=2*BOXLENGTH)&(LOWERSHADOW<=.3*BOXLENGTH)

# 7 锤头线
OUTCOLS HAMMER
TRANSLATE HAMMER 锤头线
JUDGE HAMMER (SMALLYIN|SMALLYANG)&(UPPERSHADOW<=.3*BOXLENGTH)&(LOWERSHADOW>=2*BOXLENGTH)

# 8 平底
OUTCOLS FLATFLOOR
TRANSLATE FLATFLOOR 平底
SHIFT PREV_LOW low 1
JUDGE FLATFLOOR low==PREV_LOW

# 13 连续跳空三阴线
OUTCOLS CONTINUETHREEJUMPYIN
TRANSLATE CONTINUETHREEJUMPYIN 连续跳空三阴线
SHIFT PREV_2_JUMPLOWOPEN JUMPLOWOPEN 2
SHIFT PREV_JUMPLOWOPEN JUMPLOWOPEN 1
JUDGE CONTINUETHREEJUMPYIN JUMPLOWOPEN&PREV_JUMPLOWOPEN&PREV_2_JUMPLOWOPEN&YIN&PREV_YIN&PREV_2_YIN

# 14 红三兵
OUTCOLS TRIPLEREDARMY
TRANSLATE TRIPLEREDARMY 红三兵
SHIFT PREV_2_SMALLYANG SMALLYANG 2
JUDGE TRIPLEREDARMY SMALLYANG&PREV_SMALLYANG&PREV_2_SMALLYANG&(close>PREV_CLOSE)&(PREV_CLOSE>PREV_2_CLOSE)

# 21 上涨两颗星
OUTCOLS UPWARDTWOSTARS
TRANSLATE UPWARDTWOSTARS 上涨两颗星
SHIFT PREV_2_MIDYANG MIDYANG 2
SHIFT PREV_2_LARGEYANG LARGEYANG 2
SHIFT PREV_SMALLK SMALLK 1
SHIFT PREV_BOXLOW BOXLOW 1
JUDGE UPWARDTWOSTARS (PREV_2_LARGEYANG|PREV_2_MIDYANG)&PREV_SMALLK&SMALLK&(PREV_BOXLOW>PREV_2_CLOSE)&(BOXLOW>PREV_2_CLOSE)

# 22 跳空上扬/升势鹤鸦
OUTCOLS JUMPUPWARD
TRANSLATE JUMPUPWARD 跳空上扬/升势鹤鸦
SHIFT PREV_JUMPHIGHOPEN JUMPHIGHOPEN 1
SHIFT PREV_YANG YANG 1
JUDGE JUMPUPWARD PREV_JUMPHIGHOPEN&PREV_YANG&YIN&(close>PREV_OPEN)

# 23 跳高并排阳线
OUTCOLS JUMPUPWARDYANG
TRANSLATE JUMPUPWARDYANG 跳高并排阳线
JUDGE JUMPUPWARDYANG PREV_JUMPHIGHOPEN&PREV_YANG&YANG&(open>=PREV_OPEN)

# 24 跳空下跌三颗星
OUTCOLS JUMPDOWNWARDTHREESTARS
TRANSLATE JUMPDOWNWARDTHREESTARS 跳空下跌三颗星
SHIFT PREV_2_BOXHIGH BOXHIGH 2
SHIFT PREV_BOXHIGH BOXHIGH 1
APPLYH YINHIGHMAX max PREV_2_BOXHIGH PREV_BOXHIGH BOXHIGH
SHIFT PREV_2_SMALLYIN SMALLYIN 2
SHIFT PREV_3_BOXLOW BOXLOW 3
JUDGE JUMPDOWNWARDTHREESTARS PREV_2_SMALLYIN&PREV_SMALLYIN&SMALLYIN&(YINHIGHMAX<PREV_3_BOXLOW)

# 25 上升三部曲/升势三鸦
OUTCOLS TRIPLEUPWARDS
TRANSLATE TRIPLEUPWARDS 上升三部曲/升势三鸦
SHIFT PREV_4_LARGEYANG LARGEYANG 4
SHIFT PREV_4_MIDYANG MIDYANG 4
SHIFT PREV_3_SMALLYIN SMALLYIN 3
SHIFT PREV_3_CLOSE close 3
APPLYH CLOSEMIN min PREV_3_CLOSE PREV_2_CLOSE PREV_CLOSE
SHIFT PREV_4_OPEN open 4
JUDGE TRIPLEUPWARDS (PREV_4_LARGEYANG|PREV_4_MIDYANG)&PREV_3_SMALLYIN&PREV_2_SMALLYIN&PREV_SMALLYIN&(CLOSEMIN>PREV_4_OPEN)&(LARGEYANG|MIDYANG)

# 27 两红夹一黑
OUTCOLS TWOREDONEBLACK
TRANSLATE TWOREDONEBLACK 两红夹一黑
JUDGE TWOREDONEBLACK (PREV_2_MIDYANG|PREV_2_LARGEYANG)&(PREV_MIDYIN|PREV_SMALLYIN)&(MIDYANG|LARGEYANG)

# 28 黄昏十字星
OUTCOLS TWILICROSS
TRANSLATE TWILICROSS 黄昏十字星
SHIFT PREV_2_YANG YANG 2
JUDGE TWILICROSS PREV_2_YANG&(PREV_BOXLOW>=PREV_2_CLOSE)&PREV_CROSS&YIN&(open<PREV_2_CLOSE)&(close<PREV_2_OPEN)

# 29 黄昏之星
OUTCOLS TWILISTAR
TRANSLATE TWILISTAR 黄昏之星
JUDGE TWILISTAR PREV_2_YANG&(PREV_BOXLOW>=PREV_2_CLOSE)&(PREV_SMALLYANG|PREV_SMALLYIN)&YIN&(open<PREV_2_CLOSE)&(close<PREV_2_OPEN)

# 30 淡友反攻
OUTCOLS FRIENDLYPUSHBACK
TRANSLATE FRIENDLYPUSHBACK 淡友反攻
SHIFT PREV_LARGEYANG LARGEYANG 1
SHIFT PREV_BOXLENGTH BOXLENGTH 1
JUDGE FRIENDLYPUSHBACK PREV_LARGEYANG&(LARGEYIN|MIDYIN)&(close>=PREV_OPEN+.8*PREV_BOXLENGTH)

# 31 乌云盖顶
OUTCOLS INCOMINGCLOUDS
TRANSLATE INCOMINGCLOUDS 乌云盖顶
SHIFT PREV_MIDYANG MIDYANG 1
JUDGE INCOMINGCLOUDS (PREV_LARGEYANG|PREV_MIDYANG)&(MIDYIN|LARGEYIN)&(close<=PREV_CLOSE-.5*PREV_BOXLENGTH)

# 32 倾盆大雨
OUTCOLS HEAVYRAIN
TRANSLATE HEAVYRAIN 倾盆大雨
JUDGE HEAVYRAIN (PREV_LARGEYANG|PREV_MIDYANG)&(LARGEYIN|MIDYIN)&JUMPLOWOPEN&(close<PREV_OPEN)

# 33 射击之星
OUTCOLS SHOOTINGSTAR
TRANSLATE SHOOTINGSTAR 射击之星
JUDGE SHOOTINGSTAR (SMALLYANG|SMALLYIN)&(UPPERSHADOW>=2*BOXLENGTH)&(LOWERSHADOW<.3*BOXLENGTH)

# 34 吊颈线
OUTCOLS HANGWIRE
TRANSLATE HANGWIRE 吊颈线
JUDGE HANGWIRE (SMALLYANG|SMALLYIN)&(LOWERSHADOW>=2*BOXLENGTH)&(UPPERSHADOW<.3*BOXLENGTH)

# 35 平顶
OUTCOLS FLATROOF
TRANSLATE FLATROOF 平顶
SHIFT PREV_HIGH high 1
JUDGE FLATROOF PREV_HIGH==high

# 38 双飞乌鸦
OUTCOLS DOUBLECROWS
TRANSLATE DOUBLECROWS 双飞乌鸦
JUDGE DOUBLECROWS PREV_2_YANG&PREV_YIN&YIN&(PREV_CLOSE>PREV_2_CLOSE)&(PREV_OPEN<open)&(PREV_CLOSE>close)

# 39 三只乌鸦
OUTCOLS TRIPLECROWS
TRANSLATE TRIPLECROWS 三只乌鸦
SHIFT PREV_2_MIDYIN MIDYIN 2
SHIFT PREV_2_LARGEYIN LARGEYIN 2
SHIFT PREV_2_JUMPHIGHOPEN JUMPHIGHOPEN 2
JUDGE TRIPLECROWS (PREV_2_MIDYIN|PREV_2_LARGEYIN)&(PREV_MIDYIN|PREV_LARGEYIN)&(MIDYIN|LARGEYIN)&PREV_2_JUMPHIGHOPEN&PREV_JUMPHIGHOPEN&JUMPHIGHOPEN

# 40 穿头破脚
OUTCOLS HEADSFEETYIN,HEADSFEETYANG
# 阴包阳
TRANSLATE HEADSFEETYIN 穿头破脚阴包阳
JUDGE HEADSFEETYIN PREV_YANG&YIN&(BOXHIGH>PREV_BOXHIGH)&(BOXLOW<PREV_BOXLOW)
# 阳包阴
TRANSLATE HEADSFEETYANG 穿头破脚阳包阴
JUDGE HEADSFEETYANG PREV_YIN&YANG&(BOXHIGH>PREV_BOXHIGH)&(BOXLOW<PREV_BOXLOW)

# 41 下降覆盖线
OUTCOLS DOWNWARDCOVER
TRANSLATE DOWNWARDCOVER 下降覆盖线
SHIFT PREV_2_HEADSFEETYIN HEADSFEETYIN 2
JUDGE DOWNWARDCOVER PREV_2_HEADSFEETYIN&(PREV_MIDYANG|PREV_SMALLYANG)&(MIDYIN|SMALLYIN)&(close<PREV_CLOSE)

# 43 黑三兵
OUTCOLS TRIPLEBLKARMY
TRANSLATE TRIPLEBLKARMY 黑三兵
JUDGE TRIPLEBLKARMY PREV_2_SMALLYIN&PREV_SMALLYIN&SMALLYIN&(PREV_2_CLOSE>PREV_CLOSE)&(PREV_CLOSE>close)

# 45 徐缓下跌
OUTCOLS MILDDOWNWARDS
TRANSLATE MILDDOWNWARDS 徐缓下跌
JUDGE MILDDOWNWARDS PREV_SMALLYIN&(MIDYIN|LARGEYIN)

# 48 高开出逃
OUTCOLS RUSHINGAWAY
TRANSLATE RUSHINGAWAY 高开出逃
JUDGE RUSHINGAWAY JUMPHIGHOPEN&(open>=PREV_CLOSE*1.3)&LARGEYIN

# 49 下跌三颗星
OUTCOLS DOWNWARDTRIPLESTARS
TRANSLATE DOWNWARDTRIPLESTARS 下跌三颗星
SHIFT PREV_2_HIGH high 2
APPLYH MAXTRIPLEHIGH max PREV_2_HIGH PREV_HIGH high
SHIFT PREV_3_LARGEYIN LARGEYIN 3
SHIFT PREV_3_MIDYIN MIDYIN 3
SHIFT PREV_2_SMALLK SMALLK 2
SHIFT PREV_3_LOW low 3
JUDGE DOWNWARDTRIPLESTARS (PREV_3_LARGEYIN|PREV_3_MIDYIN)&PREV_2_SMALLK&PREV_SMALLK&SMALLK&(MAXTRIPLEHIGH<PREV_3_LOW)

# 50 降势三鹤
OUTCOLS DOWNWARDTRIPLESWANS
TRANSLATE DOWNWARDTRIPLESWANS 降势三鹤
SHIFT PREV_3_HIGH high 3
APPLYH MAXTRIPLEHIGH50 max PREV_3_HIGH PREV_2_HIGH PREV_HIGH
SHIFT PREV_4_MIDYIN MIDYIN 4
SHIFT PREV_4_LARGEYIN LARGEYIN 4
SHIFT PREV_3_SMALLYANG SMALLYANG 3
JUDGE DOWNWARDTRIPLESWANS (PREV_4_MIDYIN|PREV_4_LARGEYIN)&PREV_3_SMALLYANG&PREV_2_SMALLYANG&PREV_SMALLYANG&(MAXTRIPLEHIGH50<PREV_4_OPEN)

# 52 倒三阳
OUTCOLS TRIPLEDOWNWARDSYANG
TRANSLATE TRIPLEDOWNWARDSYANG 倒三阳
SHIFT PREV_3_YIN YIN 3
JUDGE TRIPLEDOWNWARDSYANG PREV_3_YIN&PREV_2_YANG&PREV_YANG&YANG&(PREV_CLOSE<PREV_2_OPEN)&(close<PREV_OPEN)

# 53 跳空三阳线
OUTCOLS JUMPHIGHTRIPLEYANG
TRANSLATE JUMPHIGHTRIPLEYANG 跳空三阳线
JUDGE JUMPHIGHTRIPLEYANG PREV_2_YANG&PREV_YANG&YANG&PREV_2_JUMPHIGHOPEN&PREV_JUMPHIGHOPEN&JUMPHIGHOPEN

# 54 升势受阻
OUTCOLS UPWARDRESISTENCE
TRANSLATE UPWARDRESISTENCE 升势受阻
SHIFT PREV_2_BOXLENGTH BOXLENGTH 2
JUDGE UPWARDRESISTENCE PREV_2_YANG&PREV_YANG&YANG&(PREV_2_BOXLENGTH>PREV_BOXLENGTH)&(PREV_BOXLENGTH>BOXLENGTH)&(UPPERSHADOW>=.75*BOXLENGTH)

# 55 升势停顿
OUTCOLS UPWARDPAUSE
TRANSLATE UPWARDPAUSE 升势停顿
JUDGE UPWARDPAUSE SMALLYANG&(PREV_2_LARGEYANG|PREV_2_MIDYANG)&(PREV_LARGEYANG|PREV_MIDYANG)

# 56 阳线瘸腿形
OUTCOLS LAMPYANG
TRANSLATE LAMPYANG 阳线瘸腿形
JUDGE LAMPYANG PREV_2_YANG&PREV_YANG&YANG&PREV_JUMPLOWOPEN&JUMPLOWOPEN&(close<PREV_CLOSE)

# 57 两黑夹一红
OUTCOLS BLKREDBLK
TRANSLATE BLKREDBLK 两黑夹一红
JUDGE BLKREDBLK (PREV_2_LARGEYIN|PREV_2_MIDYIN)&(LARGEYIN|MIDYIN)&(PREV_MIDYANG|PREV_LARGEYANG)

# 63 长十字线
OUTCOLS LONGCROSS
TRANSLATE LONGCROSS 长十字线
JUDGE LONGCROSS CROSS&(VIBRATION>=.03*open)

# 64 螺旋桨
OUTCOLS PROPELLER
TRANSLATE PROPELLER 螺旋桨
JUDGE PROPELLER (SMALLYANG|SMALLYIN)&(VIBRATION>=3*BOXLENGTH)

# 69 尽头线
OUTCOLS EOFUPWARD,EOFDOWNWARD
# 涨势尽头线
TRANSLATE EOFUPWARD 涨势尽头线
SHIFT PREV_UPPERSHADOW UPPERSHADOW 1
SHIFT PREV_LOWERSHADOW LOWERSHADOW 1
JUDGE EOFUPWARD (PREV_LARGEYANG|PREV_MIDYANG)&(PREV_UPPERSHADOW>0)&SMALLK&(BOXLOW>=PREV_BOXHIGH)&(BOXHIGH<=PREV_HIGH)
# 跌势尽头线
TRANSLATE EOFDOWNWARD 跌势尽头线
JUDGE EOFDOWNWARD (PREV_LARGEYIN|PREV_MIDYIN)&(PREV_LOWERSHADOW>0)&SMALLK&(BOXLOW>=PREV_LOW)&(BOXHIGH>=PREV_BOXLOW)

# 70 穿头破脚
# 见 40

# 71 身怀六甲
OUTCOLS PREGNANTYANG,PREGNANTYIN
# 阳包阴
TRANSLATE PREGNANTYANG 身怀六甲阳包阴
JUDGE PREGNANTYANG PREV_YANG&YIN&(BOXHIGH<PREV_BOXHIGH)&(BOXLOW>PREV_BOXLOW)
# 阴包阳
TRANSLATE PREGNANTYIN 身怀六甲阴包阳
JUDGE PREGNANTYIN PREV_YIN&YANG&(BOXHIGH<PREV_BOXHIGH)&(BOXLOW>PREV_BOXLOW)

# 72 定海神针
OUTCOLS NIDDLEUNDERSEA
TRANSLATE NIDDLEUNDERSEA 定海神针
JUDGE NIDDLEUNDERSEA LOWERSHADOW>=2*BOXLENGTH