PLEV	FOLDER
FIN	ma
PLFDOUT candle_stick_shape_analysis
OUTCOLS date,YINX,YANGX,DYINX,DYANGX,CROSS,DRAGONFLY_CROSS,TUMBSTONE_CROSS,LONGLEG_CROSS,HAMMER
OUTCOLS SHOOTING_STAR,R_HAMMER,L_DOWN_SHADOW,L_UP_SHADOW,DOUBLE_CROSS,CTPJYINBYANG,CTPJYANGBYIN
OUTCOLS PREGNANT_BEAR,PREGNANT_BULL,PREGNANT_CROSS,PREGNANT_BULL_CROSS,SEPARATE_UPWARD,SEPARATE_DOWNWARD,SUNSET_STAR
OUTCOLS SUNSET_CROSS_STAR,MORNING_STAR,MORNING_CROSS_STAR,THREE_CROWS,THREE_RED_ARMIES

SUBC HL high low     # 高低差
SUBC OH high open
APPLYH OH abs OH    # OH 开盘价与高点距离
SUBC CL close low
APPLYH CL abs CL    # CL 收盘价与低点距离
SUBC OC open close
APPLYH OC abs OC    # OC 开盘价收盘价距离
SUBC OL open low
APPLYH OL abs OL    # OL 开盘价低点距离
SUBC CH close high
APPLYH CH abs CH    # CH 收盘价高点距离

###### 单日 #####

#阴线
JUDGE YINX data open>close
#阳线
JUDGE YANGX data open<close
# 大阴线
JUDGE DYINX data (CL<(HL*.1))&(OH<(HL*.1))&YINX
# 大阳线
JUDGE DYANGX data (OL<(HL*.1))&(CH<(HL*.1))&YANGX
# 十字星
JUDGE CROSS data open==close
# 蜻蜓十字星
JUDGE DRAGONFLY_CROSS data CROSS&(OL>=HL*.666)
# 墓碑十字星
JUDGE TUMBSTONE_CROSS data CROSS&(open==low)
# 长腿十字星
JUDGE LONGLEG_CROSS data CROSS&(OL>=HL*.4)&(CH>=HL*.4)
# 锤形
JUDGE HAMMER data (OC<=HL*.4)&((CH>=HL*.6)|(OL>=HL*.6))&((open==high)|(close==low))
# 射击之星
JUDGE SHOOTING_STAR data (OC<=HL*.4)&((CH>=HL*.6)|(OH>=HL*.6))&((open==low)|(close==low))
# 倒黑锤形
JUDGE R_HAMMER data SHOOTING_STAR&(open>close)
# 长下影线
JUDGE L_DOWN_SHADOW data (OC<=HL*.333)&((OL>=HL*.666)|(CL>=HL*.666))
# 长上影线
JUDGE L_UP_SHADOW data (OC<=HL*.3333)&((OH>=HL*.666)|(CH>=HL*.6666))

###### 两日 #####

# 双十字星
SHIFT PREV_CROSS CROSS 1
JUDGE DOUBLE_CROSS data PREV_CROSS&CROSS
# 穿头破脚阴包阳
SHIFT PREV_OPEN open 1
SHIFT PREV_CLOSE close 1
SHIFT PREV_YANGX YANGX 1
SHIFT PREV_OC OC 1
JUDGE CTPJYINBYANG data DYINX&PREV_YANGX&(open>PREV_CLOSE)&(close<PREV_OPEN)&(PREV_OC<OC*.5)
# 穿头破脚阳包阴
SHIFT PREV_YINX YINX 1
JUDGE CTPJYANGBYIN data DYANGX&PREV_YINX&(open<PREV_CLOSE)&(close>PREV_OPEN)&(PREV_OC<OC*.5)
# 熊市身怀六甲图
SHIFT PREV_DYANGX DYANGX 1
JUDGE PREGNANT_BEAR data PREV_DYANGX&YINX&(open<PREV_CLOSE)&(close>PREV_OPEN)&(OC<PREV_OC*.5)
# 牛市身怀六甲图
SHIFT PREV_DYINX DYINX 1
JUDGE PREGNANT_BULL data PREV_DYINX&YANGX&(open>PREV_CLOSE)&(close<PREV_OPEN)&(OC<PREV_OC*.5)
# 身怀六甲交叉图
JUDGE PREGNANT_CROSS data PREV_DYANGX&CROSS&(open>PREV_OPEN)&(open<PREV_CLOSE)
# 牛市身怀六甲交叉图
JUDGE PREGNANT_BULL_CROSS data PREV_DYINX&CROSS&(open>PREV_CLOSE)&(open<PREV_OPEN)
# 上升分离线图
JUDGE SEPARATE_UPWARD data PREV_YINX&YANGX&(PREV_OPEN==open)
# 下降分离线图
JUDGE SEPARATE_DOWNWARD data PREV_YANGX&YINX&(PREV_OPEN==open)

###### 三日 #####

# 晚星图
SHIFT PREV_DYANGX2 DYANGX 2
SHIFT PREV_OPEN2 open 2
SHIFT PREV_CLOSE2 close 2
JUDGE SUNSET_STAR data PREV_DYANGX2&(PREV_CLOSE2<PREV_CLOSE)&(PREV_CLOSE2<PREV_OPEN)&YINX&(close<PREV_CLOSE2)&(close>PREV_OPEN2)
# 晚十字星图
JUDEGE SUNSET_CROSS_STAR data PREV_DYANGX2&PREV_CROSS&(PREV_CLOSE2<PREV_OPEN)&YINX&(close<PREV_CLOSE2)&(close>PREV_OPEN2)
# 晨星图
SHIFT PREV_DYINX2 DYINX 2
JUDGE MORNING_STAR data PREV_DYINX2&(PREV_OPEN<PREV_CLOSE2)&(PREV_CLOSE<PREV_CLOSE2)&YANGX&(close>PREV_CLOSE2)&(close<PREV_OPEN2)
# 晨十字星图
JUDGE MORNING_CROSS_STAR data PREV_DYINX2&PREV_CROSS&(PREV_CLOSE2>PREV_OPEN)&(PREV_CLOSE2>PREV_CLOSE)&YANGX&(close>PREV_CLOSE2)&(close<PREV_OPEN2)
# 三只乌鸦
JUDGE THREE_CROWS data PREV_DYINX2&PREV_DYINX&DYINX&(PREV_OPEN>PREV_CLOSE2)&(PREV_OPEN<PREV_OPEN2)&(open>PREV_CLOSE)&(open<PREV_OPEN)&(close<PREV_CLOSE)&(PREV_CLOSE<PREV_CLOSE2)&(close<PREV_CLOSE)&(close<PREV_CLOSE2)
# 红三兵
SHIFT PREV_YANGX2 YANGX 2
JUDGE HIGH_CLOSE_PRICE_YANGX YANGX&(CH<.1*HL)
SHIFT HIGH_CLOSE_PRICE_YANGX2 HIGH_CLOSE_PRICE_YANGX 2
SHIFT HIGH_CLOSE_PRICE_YANGX1 HIGH_CLOSE_PRICE_YANGX 1
JUDGE THREE_RED_ARMIES data HIGH_CLOSE_PRICE_YANGX2&HIGH_CLOSE_PRICE_YANGX1&HIGH_CLOSE_PRICE_YANGX&(PREV_OPEN>PREV_OPEN2)&(PREV_OPEN<PREV_CLOSE2)&(open>PREV_OPEN)&(open<PREV_CLOSE)&(close>PREV_CLOSE)&(close>PREV_CLOSE2)



































































